<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8" />
  <title>shader</title>
  <style>
    body {
      margin: 0;
      padding: 0;
    }
  </style>
</head>
<body>
  <canvas id="board"></canvas>
  <script type="text/frag-shader" id="frag">
    #define max_distance 60.0
    #define epsilon 0.005
    #define max_steps 150
    #define FOV 20.0

    precision mediump float;
    uniform vec2 resolution;
    uniform float t;

    float rand(vec2 co){
      return fract(sin(dot(co.xy, vec2(12.9898,78.233))) * 43758.5453);
    }

    float floor_obj(vec3 p) {
      return p.y + 5.0;
    }

    float sphere(vec3 p, float s){
      return length(p) - s;
    }

    float box(vec3 p, vec3 b) {
      return length(max(abs(p) - b, 0.0));
    }

    float smin( float a, float b, float k ) {
        float res = exp(-k * a) + exp(-k * b);
        return -log(res) / k;
    }

    vec3 rep( vec3 p, vec3 c ) {
      return mod(p, c) - 0.5 * c;
    }

    float scene(vec3 point) {
      vec3 p = rep(point, vec3(2,2, 7));
      return min(
        smin(
          smin(
            smin(sphere(p, 0.25), box(p, vec3(1.0, 0.01, 0.1)), 24.0),
            box(p, vec3(0.01, 1.0, 0.1)), 24.0),
          box(p, vec3(0.1, 0.01, 3.5)), 24.0),
        floor_obj(point));
    }

    vec3 get_normal(vec3 point) {
      float d0 = scene(point);
      float dX = scene(point - vec3(epsilon, 0.0, 0.0));
      float dY = scene(point - vec3(0.0, epsilon, 0.0));
      float dZ = scene(point - vec3(0.0, 0.0, epsilon));

      return normalize(vec3(dX - d0, dY - d0, dZ - d0));
    }


    float raymarch(vec3 ray_origin, vec3 ray_direction) {
      float d = 0.0;

      for (int i = 0; i < max_steps; i++) {
        vec3 new_point = ray_origin + ray_direction * d;
        float s = scene(new_point);
        if (s < epsilon) return d;
        d += s;
        if (d > max_distance) return max_distance;
      }
      return max_distance;
    }

    void main() {
      float time = t / 1000.0;
      vec2 uv = ((2.0 * gl_FragCoord.xy) - resolution) / min(resolution.x, resolution.y);
      uv *= tan(radians(FOV) / 2.0);

      vec3 up = vec3(0.0, 1.0, 0.0);
      vec3 forward = vec3(0.0, 0.0, 1.0);
      vec3 right = cross(up, forward);

      vec3 ray_dir = normalize(up * uv.y + right * uv.x + forward);
      vec3 eye = vec3(cos(time * 0.1) * 0.8, sin(time * 0.2) * 0.8, (time * 4.0));
      float d = raymarch(eye, ray_dir);

      vec3 point = eye + ray_dir * d;
      vec3 point_normal = get_normal(point);
      vec3 light = eye + vec3(
        sin(time / 1.0) * 55.0,
        cos(time  / 3.0) * 44.0,
        0);
      vec3 light_dir = -normalize(light - point);
      float dotp_diffuse = max(0.0, dot(light_dir, point_normal)) * 0.8;

      if (d < max_distance) {
        float fog = 1.0 / (d / max_distance / 0.1);
        gl_FragColor = vec4(0.3, 0.6, 0.6, 1.0) * dotp_diffuse * fog;
      } else {
        float r = length(rand(uv));
        if (r > 0.997) {
          gl_FragColor = vec4(1, 1, 1, 1) * (r / 2.);
        } else {
          gl_FragColor = vec4(0, 0, 0, 1);
        }
      }
    }
  </script>
  <script type="text/vert-shader" id="vert">
    attribute vec2 position;
    void main() {
      gl_Position = vec4(position,0,1);
    }
  </script>
  <script>
    var can = document.querySelector('#board');
    can.width = window.innerWidth;
    can.height = window.innerHeight;

    var c = can.getContext('webgl');
    var w = c.drawingBufferWidth;
    var h = c.drawingBufferHeight;

    c.viewport(0, 0, w, h);
    c.clearColor(0, 0, 0, 1);
    c.clear(c.COLOR_BUFFER_BIT);

    c.blendFunc(c.SRC_ALPHA, c.ONE_MINUS_SRC_ALPHA);
    c.enable(c.BLEND);

    var fragShader = c.createShader(c.FRAGMENT_SHADER);
    var vertShader = c.createShader(c.VERTEX_SHADER);

    c.shaderSource(fragShader, document.querySelector('#frag').textContent);
    c.shaderSource(vertShader, document.querySelector('#vert').textContent);

    c.compileShader(fragShader);
    if (!c.getShaderParameter(fragShader, c.COMPILE_STATUS)) {
      console.error("frag error:", c.getShaderInfoLog(fragShader));
    }
    c.compileShader(vertShader);
    if (!c.getShaderParameter(vertShader, c.COMPILE_STATUS)) {
      console.error("vert error:", c.getShaderInfoLog(vertShader));
    }

    var program = c.createProgram();
    c.attachShader(program, fragShader);
    c.attachShader(program, vertShader);
    c.linkProgram(program);
    if(!c.getProgramParameter(program, c.LINK_STATUS)) {
      console.error('Error linking program:', c.getProgramInfoLog(program))
    }
    c.useProgram(program);

    var res = c.getUniformLocation(program, 'resolution');
    c.uniform2f(res, w, h);
    var time = c.getUniformLocation(program, 't');
    c.uniform1f(time, 0);

    var buffer = c.createBuffer();
    c.bindBuffer(c.ARRAY_BUFFER, buffer);
    c.bufferData(c.ARRAY_BUFFER, new Float32Array([
      -1, 1, -1, -1, 1, 1,
      1, 1, -1, -1, 1, -1
    ]), c.STREAM_DRAW);
    c.enableVertexAttribArray(0);
    c.vertexAttribPointer(0, 2, c.FLOAT, false, 0, 0);

    function loop (t) {
      requestAnimationFrame(loop);

      //c.clearColor(0, 0, 0, 1);
      c.clear(c.COLOR_BUFFER_BIT);

      c.uniform1f(time, t);
      c.drawArrays(c.TRIANGLES, 0, 6);
    }
    requestAnimationFrame(loop);

    //c.bindBuffer(c.ARRAY_BUFFER, null);
    //c.disableVertexAttribArray(0);
    //c.deleteBuffer(buffer);
  </script>
</body>
</html>